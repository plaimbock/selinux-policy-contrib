## <summary>Kopano collaboration platform.</summary>

######################################
## <summary>
##	Creates types and rules for a basic
##	kopano init daemon domain.
## </summary>
## <param name="prefix">
##	<summary>
##	Prefix for the domain.
##	</summary>
## </param>
#
template(`kopano_domain_template',`
	gen_require(`
		attribute kopano_domain;
	')

	##############################
	#
	# $1_t declarations
	#

	type kopano_$1_t, kopano_domain;
	type kopano_$1_exec_t;
	init_daemon_domain(kopano_$1_t, kopano_$1_exec_t)

	type kopano_$1_log_t;
	logging_log_file(kopano_$1_log_t)

	type kopano_$1_var_run_t;
	files_pid_file(kopano_$1_var_run_t)

	##############################
	#
	# $1_t local policy
	#

	manage_files_pattern(kopano_$1_t, kopano_$1_var_run_t, kopano_$1_var_run_t)
	manage_sock_files_pattern(kopano_$1_t, kopano_$1_var_run_t, kopano_$1_var_run_t)
	files_pid_filetrans(kopano_$1_t, kopano_$1_var_run_t, { file sock_file })

	manage_files_pattern(kopano_$1_t, kopano_$1_log_t, kopano_$1_log_t)
	logging_log_filetrans(kopano_$1_t, kopano_$1_log_t, { file })

	kernel_read_system_state(kopano_$1_t)

	auth_use_nsswitch(kopano_$1_t)

	logging_send_syslog_msg(kopano_$1_t)
')

######################################
## <summary>
##	Allow the specified domain to search
##	kopano configuration dirs.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`kopano_search_config',`
	gen_require(`
		type kopano_etc_t;
	')

	files_search_etc($1)
	allow $1 kopano_etc_t:dir search_dir_perms;
')

########################################
## <summary>
##	Execute a domain transition to run kopano_deliver.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`kopano_domtrans_deliver',`
	gen_require(`
		type kopano_deliver_t, kopano_deliver_exec_t;
	')

	domtrans_pattern($1, kopano_deliver_exec_t, kopano_deliver_t)
')

########################################
## <summary>
##	Execute a domain transition to run kopano_server.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`kopano_domtrans_server',`
	gen_require(`
		type kopano_server_t, kopano_server_exec_t;
	')

	domtrans_pattern($1, kopano_server_exec_t, kopano_server_t)
')

#######################################
## <summary>
##	Connect to kopano-server unix domain stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`kopano_stream_connect_server',`
	gen_require(`
		type kopano_server_t, kopano_server_var_run_t;
	')

	files_search_var_lib($1)
	stream_connect_pattern($1, kopano_server_var_run_t, kopano_server_var_run_t, kopano_server_t)
')

####################################
## <summary>
##  Allow the specified domain to manage
##  kopano /var/lib files.
## </summary>
## <param name="domain">
##  <summary>
##  Domain allowed access.
##  </summary>
## </param>
#
interface(`kopano_manage_lib_files',`
    gen_require(`
        type kopano_var_lib_t;
    ')

    files_search_var_lib($1)
    manage_files_pattern($1, kopano_var_lib_t, kopano_var_lib_t)
    manage_lnk_files_pattern($1, kopano_var_lib_t, kopano_var_lib_t)
    manage_dirs_pattern($1, kopano_var_lib_t, kopano_var_lib_t)
')
